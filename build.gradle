plugins {
    id 'com.github.ben-manes.versions' version '0.15.0'
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'org.sonarqube' version '2.5'
}

apply plugin: 'maven'
apply plugin: 'osgi'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'checkstyle'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8


group = 'org.trellisldp'
description = 'Trellis Vocabularies'

ext {
    moduleName = 'org.trellisldp.vocabulary'
    vendor = 'Trellis LDP'
    homepage = 'http://www.trellisldp.org'
    docURL = 'http://www.trellisldp.org/apidocs'
    license = 'Apache 2'

    /* Dependencies */
    commonsRdfVersion = '0.3.0-incubating'

    /* Testing */
    junitVersion = '5.0.1'
    junitPlatformVersion = '1.0.1'
    mockitoVersion = '2.11.0'
    jenaVersion = '3.4.0'
    xercesVersion = '2.11.0'
    logbackVersion = '1.2.3'
    jacocoVersion = "0.7.9"
    apiguardianVersion = '1.0.0'

    /* OSGi */
    spiflyVersion = '1.0.8'
    projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")
}

configurations {
    buildToolsConfig
}

dependencies {
    api group: 'org.apache.commons', name: 'commons-rdf-api', version: commonsRdfVersion

    testImplementation group: 'org.apache.commons', name: 'commons-rdf-jena', version: commonsRdfVersion
    testImplementation group: 'org.apache.jena', name: 'jena-osgi', version: jenaVersion
    testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: apiguardianVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'xerces', name: 'xercesImpl', version: xercesVersion

    testRuntimeClasspath group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion

    buildToolsConfig 'org.trellisldp:trellis-build-tools:0.2.0'
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

release {
    tagTemplate = '$name-$version'
}

jar {
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "${moduleName};version=${projectOsgiVersion}"
        instruction 'Require-Capability', '''\
            osgi.extender; filter:="(osgi.extender=osgi.serviceloader.processor)",
            osgi.serviceloader;
                filter:="(osgi.serviceloader=org.apache.commons.rdf.api.RDF)";
                resolution:=optional; cardinality:=at_least_one
            '''
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar
    archives sourceJar
    archives (file('build/resources/main/features.xml')) {
        classifier 'features'
        type 'xml'
    }
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

license {
    include "**/*.java"
    header rootProject.file('build/license/HEADER.txt')
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

processResources {
    outputs.upToDateWhen { false }
    expand project.properties
}

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
        def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ""
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            packaging 'jar'
            url 'http://www.trellisldp.org'
            inceptionYear '2017'
            name 'Trellis Repository Vocabularies'
            description 'The core vocabularies used in a trellis repository'

            organization {
                name project.vendor
                url project.homepage
            }

            developers {
                developer {
                    id 'acoburn'
                    name 'Aaron Coburn'
                    email 'acoburn @ (domain of organization url)'
                    organization 'Amherst College'
                    organizationUrl 'https://www.amherst.edu'
                    roles {
                        role 'developer'
                    }
                    timezone '-5'
                }
            }

            scm {
                connection 'scm:git:git://github.com/trellis-ldp/trellis-vocabulary.git'
                developerConnection 'scm:git:git@github.com/trellis-ldp/trellis-vocabulary.git'
                url 'https://github.com/trellis-ldp/trellis-vocabulary'
                tag 'HEAD'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                    comments 'Copyright (c) 2017 Trellis LDP'
                }
            }
        }
    }
}

task processBuildTools(type: Copy) {
    from {
        configurations.buildToolsConfig.collect {
            zipTree(it).matching {
                include 'checkstyle/*.xml'
                include 'license/*.txt'
            }
        }
    }
    into 'build'
}

checkstyle {
    configFile = rootProject.file('build/checkstyle/checkstyle.xml')
    configProperties.checkstyleConfigDir = rootProject.file('build/checkstyle/')
    toolVersion = '8.1'
}

sonarqube {
    properties {
        property "sonar.projectName", "Trellis Repository Vocabularies"
        property "sonar.projectKey", "org.trellisldp:trellis-vocabulary"
        property "sonar.links.homepage", "https://github.com/trellis-ldp/trellis"
        property "sonar.links.issue", "https://github.com/trellis-ldp/trellis-vocabulary/issues"
        property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/trellis-vocabulary.git"
    }
}

task docs(type: Javadoc) {
    outputs.upToDateWhen { false }
    source sourceSets.main.allJava
    classpath = files(sourceSets.main.compileClasspath)
    destinationDir = new File(projectDir, "docs/${version}")
    options {
        links "http://docs.oracle.com/javase/8/docs/api/"
        links "https://commons.apache.org/proper/commons-rdf/apidocs/"
    }
}

checkstyleMain.dependsOn processBuildTools
afterReleaseBuild.dependsOn uploadArchives
afterReleaseBuild.dependsOn docs

